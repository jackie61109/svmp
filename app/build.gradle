import java.text.SimpleDateFormat

apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'io.fabric'

apply plugin: 'kotlin-kapt'

repositories {
    maven { url 'https://maven.fabric.io/public' }
    google()
}

def final BUILD_NUMBER = System.getenv("BUILD_NUMBER")

static def gitCommitHash() {
    def cmd = 'git rev-parse --short HEAD'
    cmd.execute().text.trim()
}

static def getDateTime() {
    SimpleDateFormat df = new SimpleDateFormat("YYYYMMddHHmm")

    return df.format(new Date())
}

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.fet.svmp"
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 1
        versionName "1.0.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        flavorDimensions "default"
        buildConfigField "String", "BUILD_NUMBER", "\"$BUILD_NUMBER\""
    }

    dataBinding {
        enabled = true
    }

    signingConfigs {
        keyConfig {
            storeFile file("FetSVMP.jks")
            storePassword "svmp12345"
            keyAlias "FetSVMP"
            keyPassword "svmp12345"
        }
        unsignedKeyConfig {
            storePassword ""
            keyAlias ""
            keyPassword ""
        }
    }

    buildTypes {
        debug {
            buildConfigField "boolean", "IS_DEBUG", "true"
            matchingFallbacks = ['debug']
            aaptOptions.cruncherEnabled = false
            ext.alwaysUpdateBuildId = false

            signingConfig signingConfigs.keyConfig
        }

        release {
            buildConfigField "boolean", "IS_DEBUG", "false"
            matchingFallbacks = ['release']
            signingConfig signingConfigs.keyConfig
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    android.applicationVariants.all { variant ->
        variant.outputs.all {
            def final PROJECT_NAME = "ToSVMP"
            def final SEPARATOR = "-"
            def final EXTENSION = ".apk"
            def flavor = variant.productFlavors[0].name
            def buildType = variant.buildType.name
            def commit = gitCommitHash()
            def versionName = variant.versionName
            def buildTime = getDateTime()

            def newApkName = PROJECT_NAME +
                        SEPARATOR + flavor +
                        SEPARATOR + buildType +
                        SEPARATOR + buildTime +
                        SEPARATOR + commit +
                        SEPARATOR + versionName + EXTENSION

            outputFileName = new File(newApkName)
        }
    }
    productFlavors {
        sit {
            manifestPlaceholders = [appName: "SVMP SIT", fabricApiKey: "7e5c897e30de146e81e3ebfbdb03b830c89b5d39"]

            dimension "default"
            buildConfigField "String", "WALLET_ENVIRONMENT", '"SIT"'
        }

        prod {
            manifestPlaceholders = [appName: "SVMP Production", fabricApiKey: "7e5c897e30de146e81e3ebfbdb03b830c89b5d39"]

            dimension "default"
            buildConfigField "String", "WALLET_ENVIRONMENT", '"PROD"'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'com.android.support:design:28.0.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'

    //Fabric CrashLytics
    implementation('com.crashlytics.sdk.android:crashlytics:2.9.6@aar') {
        transitive = true
    }
    //Logger third party
    implementation 'com.orhanobut:logger:2.1.1'
    //permission
    implementation 'pub.devrel:easypermissions:0.4.0'
    //FireBase
    implementation 'com.google.firebase:firebase-core:16.0.6'
    implementation 'com.google.firebase:firebase-messaging:17.3.4'
    ///////// Architecture components /////////

    implementation "android.arch.lifecycle:extensions:1.1.1"
    // alternatively, just ViewModel
    implementation "android.arch.lifecycle:viewmodel:1.1.1"
    // alternatively, just LiveData
    implementation "android.arch.lifecycle:livedata:1.1.1"

    annotationProcessor "android.arch.lifecycle:compiler:1.1.1"

    // Room (use 1.1.0-beta2 for latest beta)
    implementation "android.arch.persistence.room:runtime:1.1.1"
    kapt "android.arch.persistence.room:compiler:1.1.1"

    // Paging
    implementation "android.arch.paging:runtime:1.0.1"

    // Test helpers for LiveData
    testImplementation "android.arch.core:core-testing:1.1.1"

    // Test helpers for Room
    testImplementation "android.arch.persistence.room:testing:1.1.1"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:0.27.0-eap13"

    ///////// Architecture components /////////
}

apply plugin: 'com.google.gms.google-services'
